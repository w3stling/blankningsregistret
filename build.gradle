plugins {
    id 'org.sonarqube' version '2.6.2'
    id 'com.jfrog.bintray' version '1.8.1'
    id 'com.jfrog.artifactory' version '4.7.3'
    id 'org.ajoberstar.git-publish' version '1.0.1'
    id 'net.researchgate.release' version '2.7.0'
    id 'me.champeau.gradle.jmh' version '0.4.5'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'distribution'
apply plugin: 'me.champeau.gradle.jmh'

group 'com.apptastic'
version "${version}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()

    maven {
        url  "https://jcenter.bintray.com"
    }
}

dependencies {
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.15.0'
}

distTar {
    compression = Compression.GZIP
    extension = 'tar.gz'
}

distributions {
    main {
        contents {
            from("$projectDir")
            exclude('.gitignore')
            exclude('.travis.yml')
            exclude('*.iml')
            exclude('out')
            exclude('build')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    if (!project.version.endsWith("-SNAPSHOT")) {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publications = ['BlankningsregistretPublication']
        dryRun = false
        publish = true
        override = true

        pkg {
            repo = 'maven-repo'
            name = 'blankningsregistret'
            desc = 'Java library for extracting data from the Financial Supervisory Authority (Finansinspektionen) short selling registry.'
            issueTrackerUrl = 'https://github.com/w3stling/blankningsregistret/issues'
            vcsUrl = 'https://github.com/w3stling/blankningsregistret.git'
            licenses = ['MIT']

            version {
                name = System.properties['version']
                released = new Date()
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            if (project.version.endsWith("-SNAPSHOT")) {
                repoKey = 'oss-snapshot-local'
            } else {
                repoKey = 'oss-release-local'
            }
            username = System.getenv('ARTIFACTORY_USERNAME')
            password = System.getenv('ARTIFACTORY_PASSWORD')
            maven = true
        }
        defaults {
            publications ('BlankningsregistretPublication')
        }
    }
}

publishing {
    publications {
        BlankningsregistretPublication(MavenPublication) {
            from components.java
            groupId 'com.apptastic'
            artifactId 'blankningsregistret'
            version "${version}"

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

gitPublish {
    repoUri = 'https://github.com/w3stling/blankningsregistret.git'
    branch = 'gh-pages'

    contents {
        from("$buildDir/docs/javadoc") {
            into "javadoc/${version}"
        }
    }

    preserve {
        include 'javadoc/**'
        exclude 'javadoc/*-SNAPSHOT/**'
    }

    commitMessage = 'Publishing javadoc'
}

release {
    tagTemplate = 'v$version'
}
